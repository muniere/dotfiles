#compdef gtr git-gtr
#
# Zsh completion for gtr / git-gtr direct invocation.
#
# _git_gtr (loaded via _git dispatch) breaks under direct invocation
# because _describe does not work when words array is mutated.
# This file provides standalone completion using compadd.
#
# See: https://github.com/coderabbitai/git-worktree-runner/issues/83

_gtr() {
  emulate -L zsh

  local -a commands
  commands=(
    new go run copy rm mv rename editor ai
    ls list clean doctor adapter config completion init version help
  )

  local -a command_descs
  command_descs=(
    'new        -- Create a new worktree'
    'go         -- Navigate to worktree'
    'run        -- Execute command in worktree'
    'copy       -- Copy files between worktrees'
    'rm         -- Remove worktree(s)'
    'mv         -- Rename worktree and branch'
    'rename     -- Rename worktree and branch'
    'editor     -- Open worktree in editor'
    'ai         -- Start AI coding tool'
    'ls         -- List all worktrees'
    'list       -- List all worktrees'
    'clean      -- Remove stale worktrees'
    'doctor     -- Health check'
    'adapter    -- List available adapters'
    'config     -- Manage configuration'
    'completion -- Generate shell completions'
    'init       -- Generate shell integration for cd support'
    'version    -- Show version'
    'help       -- Show help'
  )

  local -a branches all_options
  branches=(${(f)"$(git branch --format='%(refname:short)' 2>/dev/null)"})
  all_options=("1" "${branches[@]}")

  local subcmd="$words[2]"

  # Complete subcommand name
  if (( CURRENT == 2 )); then
    compadd -l -d command_descs -a commands
    return
  fi

  # Complete subcommand arguments
  case "$subcmd" in
    new)
      _arguments \
        '1:branch name:' \
        '--from[Base ref]:ref:' \
        '--from-current[Create from current branch]' \
        '--track[Track mode]:mode:(auto remote local none)' \
        '--no-copy[Skip file copying]' \
        '--no-fetch[Skip git fetch]' \
        '--no-hooks[Skip post-create hooks]' \
        '--force[Allow same branch in multiple worktrees]' \
        '--name[Custom folder name suffix]:name:' \
        '--folder[Custom folder name (replaces default)]:folder:' \
        '--yes[Non-interactive mode]' \
        '--editor[Open in editor after creation]' \
        '-e[Open in editor after creation]' \
        '--ai[Start AI tool after creation]' \
        '-a[Start AI tool after creation]'
      ;;
    clean)
      _arguments \
        '--merged[Remove worktrees with merged PRs/MRs]' \
        '--yes[Skip confirmation prompts]' \
        '-y[Skip confirmation prompts]' \
        '--dry-run[Show what would be removed]' \
        '-n[Show what would be removed]'
      ;;
    list|ls)
      _arguments '--porcelain[Machine-readable output]'
      ;;
    go|run|rm|mv|rename|copy|editor|ai)
      if (( CURRENT == 3 )); then
        compadd -a all_options
      else
        case "$subcmd" in
          editor)
            _arguments '--editor[Editor to use]:editor:(atom cursor emacs idea nano none nvim pycharm sublime vim vscode webstorm zed)'
            ;;
          ai)
            _arguments '--ai[AI tool to use]:tool:(aider auggie claude codex continue copilot cursor gemini none opencode)'
            ;;
          rm)
            _arguments \
              '--delete-branch[Delete branch]' \
              '--force[Force removal even if dirty]' \
              '--yes[Non-interactive mode]'
            ;;
          mv|rename)
            _arguments \
              '--force[Force move even if locked]' \
              '--yes[Skip confirmation]'
            ;;
          copy)
            _arguments \
              '-n[Dry-run preview]' \
              '--dry-run[Preview without copying]' \
              '-a[Copy to all worktrees]' \
              '--all[Copy to all worktrees]' \
              '--from[Source worktree]:source:->worktrees' \
              '*:target:->worktrees'
            case "$state" in
              worktrees) compadd -a all_options ;;
            esac
            ;;
        esac
      fi
      ;;
    config)
      local config_action=""
      local arg
      for arg in "${words[@]}"; do
        case "$arg" in
          list|get|set|add|unset)
            [[ -z "$config_action" ]] && config_action="$arg"
            ;;
        esac
      done

      local -a config_keys
      config_keys=(
        gtr.copy.include gtr.copy.exclude gtr.copy.includeDirs gtr.copy.excludeDirs
        gtr.hook.postCreate gtr.hook.preRemove gtr.hook.postRemove gtr.hook.postCd
        gtr.editor.default gtr.editor.workspace gtr.ai.default
        gtr.worktrees.dir gtr.worktrees.prefix gtr.defaultBranch gtr.provider gtr.ui.color
      )

      if [[ -z "$config_action" ]]; then
        _values 'config action' list get set add unset --local --global --system
      else
        case "$config_action" in
          list|get)
            _arguments \
              '--local[Use local git config]' \
              '--global[Use global git config]' \
              '--system[Use system git config]' \
              "*:config key:($config_keys)"
            ;;
          set|add|unset)
            _arguments \
              '--local[Use local git config]' \
              '--global[Use global git config]' \
              "*:config key:($config_keys)"
            ;;
        esac
      fi
      ;;
    completion|init)
      _values 'shell' bash zsh fish
      ;;
  esac
}

_gtr "$@"
