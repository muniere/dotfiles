#compdef git-gtr gtr
# Zsh completion for git gtr (Git Worktree Runner)
#
# Based on upstream _git-gtr with the following changes:
# - #compdef: removed `_git-gtr` (not a valid command name)
# - Function name: _git_gtr (underscore, for _git dispatch compatibility)
# - emulate -L zsh (prevents breakage under ksh emulation from _git)
# - words normalization: _git dispatch (words[1]==git) only adjusts CURRENT
# - (list|ls) pattern: expanded to avoid parse error
# - Self-call: _git_gtr
#
# See: https://github.com/coderabbitai/git-worktree-runner/issues/83

_git_gtr() {
  emulate -L zsh

  # Normalize invocation so we always see: [git, gtr, <command>, ...]
  # Handles: `git gtr`, `gtr`, and `git-gtr` invocations
  if [[ $words[1] == git ]]; then
    # _git dispatch: words already (git gtr ...) but CURRENT is off by 1
    (( CURRENT += 1 ))
  else
    # Handle both `gtr ...` and `git-gtr ...`
    words=(git gtr "${words[@]:1}")
    (( CURRENT += 1 ))
  fi

  local -a commands
  commands=(
    'new:Create a new worktree'
    'go:Navigate to worktree'
    'run:Execute command in worktree'
    'copy:Copy files between worktrees'
    'rm:Remove worktree(s)'
    'mv:Rename worktree and branch'
    'rename:Rename worktree and branch'
    'editor:Open worktree in editor'
    'ai:Start AI coding tool'
    'ls:List all worktrees'
    'list:List all worktrees'
    'clean:Remove stale worktrees'
    'doctor:Health check'
    'adapter:List available adapters'
    'config:Manage configuration'
    'completion:Generate shell completions'
    'init:Generate shell integration for cd support'
    'version:Show version'
    'help:Show help'
  )

  local -a branches all_options
  # Get branch names
  branches=(${(f)"$(git branch --format='%(refname:short)' 2>/dev/null)"})
  # Add special ID '1' for main repo
  all_options=("1" "${branches[@]}")

  # Early handler for `new` command - handle all positions
  if (( CURRENT >= 4 )) && [[ $words[3] == new ]]; then
    _arguments \
      '1:branch name:' \
      '--from[Base ref]:ref:' \
      '--from-current[Create from current branch]' \
      '--track[Track mode]:mode:(auto remote local none)' \
      '--no-copy[Skip file copying]' \
      '--no-fetch[Skip git fetch]' \
      '--no-hooks[Skip post-create hooks]' \
      '--force[Allow same branch in multiple worktrees]' \
      '--name[Custom folder name suffix]:name:' \
      '--folder[Custom folder name (replaces default)]:folder:' \
      '--yes[Non-interactive mode]' \
      '--editor[Open in editor after creation]' \
      '-e[Open in editor after creation]' \
      '--ai[Start AI tool after creation]' \
      '-a[Start AI tool after creation]'
    return
  fi

  # Early handler for `clean` command
  if (( CURRENT >= 4 )) && [[ $words[3] == clean ]]; then
    _arguments \
      '--merged[Remove worktrees with merged PRs/MRs]' \
      '--yes[Skip confirmation prompts]' \
      '-y[Skip confirmation prompts]' \
      '--dry-run[Show what would be removed]' \
      '-n[Show what would be removed]'
    return
  fi

  # Early handler for `list/ls` command - add --porcelain
  if (( CURRENT >= 4 )) && [[ $words[3] == list || $words[3] == ls ]]; then
    _arguments '--porcelain[Machine-readable output]'
    return
  fi

  # Complete the gtr subcommand (new, go, editor, etc.)
  if (( CURRENT == 3 )); then
    _describe 'commands' commands
  # Complete arguments to the subcommand
  elif (( CURRENT == 4 )); then
    case "$words[3]" in
      go|run|rm|mv|rename|copy)
        _describe 'branch names' all_options
        ;;
      editor)
        _describe 'branch names' all_options
        ;;
      ai)
        _describe 'branch names' all_options
        ;;
      config)
        # Complete action or scope flags
        _values 'config action' list get set add unset --local --global --system
        ;;
      completion|init)
        # Complete shell names
        _values 'shell' bash zsh fish
        ;;
    esac
  # Complete subsequent arguments
  elif (( CURRENT >= 5 )); then
    case "$words[3]" in
      editor)
        _arguments '--editor[Editor to use]:editor:(atom cursor emacs idea nano none nvim pycharm sublime vim vscode webstorm zed)'
        ;;
      ai)
        _arguments '--ai[AI tool to use]:tool:(aider auggie claude codex continue copilot cursor gemini none opencode)'
        ;;
      rm)
        _arguments \
          '--delete-branch[Delete branch]' \
          '--force[Force removal even if dirty]' \
          '--yes[Non-interactive mode]'
        ;;
      mv|rename)
        _arguments \
          '--force[Force move even if locked]' \
          '--yes[Skip confirmation]'
        ;;
      copy)
        _arguments \
          '-n[Dry-run preview]' \
          '--dry-run[Preview without copying]' \
          '-a[Copy to all worktrees]' \
          '--all[Copy to all worktrees]' \
          '--from[Source worktree]:source:->worktrees' \
          '*:target:->worktrees'
        case "$state" in
          worktrees) _describe 'branch names' all_options ;;
        esac
        ;;
      config)
        # Find action by scanning all config args (handles flexible flag positioning)
        # Use offset 3 to start from words[4] (first arg after 'config')
        # Zsh uses 0-based offset: ${array[@]:3} = elements starting from 4th position
        local config_action=""
        local arg
        for arg in "${words[@]:3}"; do
          case "$arg" in
            list|get|set|add|unset)
              [[ -z "$config_action" ]] && config_action="$arg"
              ;;
          esac
        done

        if [[ -z "$config_action" ]]; then
          # Still need action or scope
          _values 'config action' list get set add unset --local --global --system
        else
          case "$config_action" in
            list|get)
              # Read operations support all scopes including --system
              _arguments \
                '--local[Use local git config]' \
                '--global[Use global git config]' \
                '--system[Use system git config]' \
                '*:config key:(gtr.copy.include gtr.copy.exclude gtr.copy.includeDirs gtr.copy.excludeDirs gtr.hook.postCreate gtr.hook.preRemove gtr.hook.postRemove gtr.hook.postCd gtr.editor.default gtr.editor.workspace gtr.ai.default gtr.worktrees.dir gtr.worktrees.prefix gtr.defaultBranch gtr.provider gtr.ui.color)'
              ;;
            set|add|unset)
              # Write operations only support --local and --global
              # (--system may require root or appropriate file permissions)
              _arguments \
                '--local[Use local git config]' \
                '--global[Use global git config]' \
                '*:config key:(gtr.copy.include gtr.copy.exclude gtr.copy.includeDirs gtr.copy.excludeDirs gtr.hook.postCreate gtr.hook.preRemove gtr.hook.postRemove gtr.hook.postCd gtr.editor.default gtr.editor.workspace gtr.ai.default gtr.worktrees.dir gtr.worktrees.prefix gtr.defaultBranch gtr.provider gtr.ui.color)'
              ;;
          esac
        fi
        ;;
    esac
  fi
}

_git_gtr "$@"
